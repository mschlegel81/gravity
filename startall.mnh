#!C:\bin\mnh.exe -quiet -convertPrintToLog -logDateFmt hh:mm:ss.zzz -logLocationLength 20 +log stdOut(2)
wt:='C:\Users\mschl\AppData\Local\Microsoft\WindowsApps\wt.exe';// -d %CD% -p "grav 32" cmd /c grav32.exe'

parameters(execName:String,closeAfter:Boolean,replay:Boolean)->
  ['-d',execName.extractFileDirectory.systemSpecificFilename,
   '-p','"'&execName&'"',
   'cmd',closeAfter ? '/c' : '/k',execName,
   execName.matches('low_density') ? 'ld' :
   execName.matches('high_density') ? 'hd' : void,
   replay ? 'replay' : void];
memoized gravExes->allFiles('.','grav*.exe').map(::expandedFileName).map(::systemSpecificFilename);
memoized mnhFiles->allFiles('.','*.mnh').filter((f)->f.extractFileDirectory<>'.' AND f.matches('grav\d+\.mnh'));

runningGravTasks->getTaskInfo.map((i)->gravExes[!pos(gravExes,i['commandLine']).isInfinite][0]).toSet;

start(closeAfter:Boolean,replay:Boolean)->begin
  local alreadyRunning:=runningGravTasks;

  local load:=[getCPULoadPercentage];
  localAsync({while(true,load:=(load | sleep(1) | getCPULoadPercentage).trailing(16))});

  local startedCount:=0;
  gravExes
  .sort
  .map((n)->n.extractFileNameOnly.clean(['0'..'9'],'').softCast=>n)
  .sort(1).reverseList.sort(0).reverseList.getInner(1)
  .each(execName,execName in alreadyRunning
                 ? log('          ',execName,' is already running')
                 : begin
                     while(load.size<16 OR load.max>=75,sleep(1));
                     log('Starting: ',execName);
                     assert(exec(wt,parameters(execName,closeAfter,replay))[1]=0);
                     startedCount+=1;
                     while(execName not in (alreadyRunning:=runningGravTasks),sleep(0.1));
                   end);
  log(startedCount,' tasks started');
  alreadyRunning:=getTaskInfo.map((i)->i['commandLine']).filter((cl)->!pos(gravExes,cl).isInfinite.agg(and));
  log(alreadyRunning.size,' tasks are running: ',join("\n  "&sort(alreadyRunning)));
end;

//*Start and close after finish
main->start(true,false);

//*Start and keep open
main('k')->start(false,false);

//*Start in replay mode
main('r')->begin
  mnhFiles.pEach(script,execPipeless(executor,[script]));
  start(true,true);
end;

main('list')->printf('%s',gravExes.sort | mnhFiles.sort);
