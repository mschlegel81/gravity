execute(execName:String,replay:Boolean)->
  execAsync(execName,[execName.matches('low_density') ? 'ld' :
                      execName.matches('high_density') ? 'hd' : void,
                      replay ? 'replay' : void].{$L==[] ? void : $L});

synchronized gravExes->begin
  local result:=void;
  local resultTime:=-600;
save;
  scriptTime>resultTime+60
  ? begin
      result:=allFiles('.','grav*.exe').map(::expandedFileName).map(::systemSpecificFilename);
      resultTime:=scriptTime;
    end
  : void;
  result;
end;

memoized mnhFiles->allFiles('.','*.mnh').filter((f)->f.extractFileDirectory<>'.' AND f.matches('grav\d+\.mnh'));

runningGravTasks->getTaskInfo.map((i)->gravExes[!pos(gravExes,i['commandLine']).isInfinite][0]).toSet;

mutable toClear:=0;
tPrint(...)->begin
  local out:=join([formatTime("hh:mm.ss",systime),' ']|...);
  out&=repeat(' ',toClear-out.length);
  print("\r",out);
  toClear:=0;
end;

tPrintD(...)->begin
  local out:=join([formatTime("hh:mm.ss",systime),' ']|...);
  out&=repeat(' ',toClear-(toClear:=out.length));
  printDirect("\r",out);
end;

BLOCKS:=[0..100].map((l)->[" ","▁","▂","▃","▄","▅","▆","▇","█"][floor(l*9/101)])|'!';

SKIP:='skip';
FORCE:='force';
QUIT:='quit';

exeSorting(L:StringCollection)->L.map((n)->n.extractFileNameOnly.clean(['0'..'9'],'').softCast=>n).sort(1).sort(0).getInner(1);


start(replay:Boolean)->begin
  assertUniqueInstance;
  local CPU_threshold:=replay ? 85 : 50;
  local alreadyRunning:=runningGravTasks;
  local firstQuestion:=true;
  local userSignal:='';
  localAsync({while(true,begin
    userSignal:=ask('Next element in queue...',[SKIP,FORCE,QUIT],firstQuestion);
    firstQuestion:=false;
  end)});

  local load:=[getCPULoadPercentage];
  localAsync({while(true,begin
    sleep(0.1);
    local c:=getCPULoadPercentage;
    load:=(load|c).trailing(32);
  end)});

  local startedCount:=0;
  gravExes
  .exeSorting
  .each(execName,!fileExists(execName)      ? tPrint('          ',execName.relativeFileName,' has been deleted') :
                 execName in alreadyRunning ? tPrint('          ',execName.relativeFileName,' is already running')
                 : begin
                     userSignal:=userSignal==QUIT ? QUIT : '';
                     while((load.size<16 OR max(load-[load.size-1..0])>=CPU_threshold) AND (userSignal==''),begin
                       tPrintD('Next:     ',execName.relativeFileName,"; CPU: ",BLOCKS[load].join,load.trailing,'%');
                       sleep(0.1);
                     end);
                     userSignal in [SKIP,QUIT]
                     ? tPrint('Skipping: ',execName.relativeFileName)
                     : begin
                         userSignal==FORCE
                         ? tPrint('Forced:   ',execName.relativeFileName)
                         : tPrint('Starting: ',execName.relativeFileName);
                         execute(execName,replay);
                         startedCount+=1;
                         load|=min(101,load.trailing+(100 div mnhInfo['configured_cpus']));
                         while(execName not in (alreadyRunning:=runningGravTasks),sleep(0.1));
                       end;
                   end);
  tPrint(startedCount,' tasks started');
  alreadyRunning:=getTaskInfo.map((i)->i['commandLine']).filter((cl)->!pos(gravExes,cl).isInfinite.agg(and));
  tPrint(alreadyRunning.size,' tasks are running: ',join("\n  "&sort(alreadyRunning)));
end;

//*Start in calculation mode
main->start(false);

//*Start in replay mode
main('r')->start(true);

//*Decompress all compressed files
main('d')->mnhFiles.pEach(script,begin
  print('Starting ',script);
  assert(exec(executor,[script])[1]==0,'Script ',script,' failed');
  print(script,' executed successfully');
end);

//*Decompress compressed files matching pattern
main('d',pattern)->mnhFiles.filter((script)->script.matches(pattern)).pEach(script,begin
  print('Starting ',script);
  assert(exec(executor,[script])[1]==0,'Script ',script,' failed');
  print(script,' executed successfully');
end);

//*List executables (exe) and compressed executables (mnh)
main('list')->begin
  local f:=(gravExes | mnhFiles).expandedFileName.relativeFileName;
  f:=[f.extractFileDirectory,f.extractFileName].transpose;
  local allNames      :=f.getInner(1).unique;

  f.getInner(0).unique.each(dir,join([dir,': ']|allNames.map((n)->[dir,n] in f ? n : ''),"\t"))
   .join("\n").print;
end;

watcher(filename:String)->{begin
  local it:=fileLineIterator(filename,600);
  local trailSize:=100;
  local trail:=[];
  localAsync({begin
    local n:=void;
    while(!isVoid(n:=it()),trail:=trailing(trail|("\t"&n),trailSize));
  end});
save;
  trailSize:=max(1,$trailSize);
  trail.size>trailSize
  ? trail.trailing(trailSize)
  : trail;
end};

//*Watch all log files
main('w')->begin
  local killSignal:=false;
  local linesTotal:=1;
  local load:=[getCPULoadPercentage];
  local cpuWidth:=32;
  localAsync({while(!killSignal,load:=trailing(load|(sleep(0.5) orElse getCPULoadPercentage),cpuWidth))});

  localAsync({begin local answer:=''; while(answer!=QUIT,begin
    answer:=ask('?',[QUIT,'+','-'],false);
    answer=='+' ? printDirect("\rLines: ",linesTotal+=1,'  ') :
    answer=='-' ? printDirect("\rLines: ",linesTotal:=max(1,linesTotal-1),'  ') :
    answer==QUIT ? killSignal:=true : void;
  end); end});
  local logs:=runningGravTasks
    .exeSorting
    .filter((e)->e.changeFileExt('.log').fileExists)
    .map((e)->begin
       local key:=e.relativeFileName.changeFileExt('');
       [key,watcher(e.changeFileExt('.log')),['-']];
     end);

  sleep(1);

  local nextScanForNewTasks:=scriptTime+10;
  local sleepTime:=0;
  while(!killSignal,begin
    local anyChanged:=false;
    scriptTime>=nextScanForNewTasks
    ? begin
        local currentRunning:=runningGravTasks.exeSorting.filter((e)->e.changeFileExt('.log').fileExists);
        currentRunning.map((e)->e.relativeFileName.changeFileExt('')).sort == logs.getInner(0).sort
        ? void
        : begin
            local previousEntries:=logs.map((l)->l[0]=>l).toMap;
            logs:=currentRunning
                  .map((e)->begin
                     local key:=e.relativeFileName.changeFileExt('');
                     previousEntries[key] orElse [key,watcher(e.changeFileExt('.log')),['']];
                   end);
            anyChanged:=true;
          end;
        nextScanForNewTasks:=scriptTime+10;
      end
    : void;

    logs:=logs.each(e,
      begin
        (local n:=e[1](linesTotal))==e[2]
        ? e
        : begin
            anyChanged:=true;
            [e[0],e[1],n];
          end;
      end);

    anyChanged
    ? begin
        local out:=logs.map((l)->[l[0],l[2]==[] ? [''] : l[2]]);
        out.size==0 ? void : begin
          local lines:=out.getInner(1).map(::size).agg(+);
          while(lines>max(linesTotal,out.size),begin
            local k:=argMax(out.getInner(1).map(::size));
            out[k]:=begin
                      lines-=1;
                      [out[k,0],out[k,1].tail];
                    end;
          end);
          cpuWidth:=1+out.getInner(0)        .map(::length).max
                     +out.getInner(1).flatten.map(::length).max
                   -9;
          out:=out.each(o,o[0]&o[1].head,o[1].tail,|);
        end;
        while(out.size<linesTotal,out|="\t...");
        clearPrint;
        printf('%s',out);
      end
    : void;
    printDirect("\rCPU ",BLOCKS[load].join,load.trailing,'% ');

    anyChanged ? sleepTime:=0 : sleep(sleepTime:=1-(1-sleepTime)*0.9);
  end);
end;

