USE cpuMonitor;

MAX_GRAV_INSTANCES:=12;
datastore occupiedIndexes:=[].toMap;

positionParameter(execName:String)->begin
  local i:=0;
  local running:=runningGravTasks;
  occupiedIndexes.getInner(0).minus(running).each(key,occupiedIndexes[key]:=void);
  local occupied:=occupiedIndexes.getInner(1);
  while(i in occupied,i+=1);

  occupiedIndexes[execName]:=i;
  writeDataStores;
  'pos='&i;
end;

execute(execName:String,replay:Boolean)->
  execAsync(execName,[execName.matches('low_density') ? 'ld' :
                      execName.matches('high_density') ? 'hd' : void,
                      replay ? 'replay' : void,
                      positionParameter(execName)].{$L==[] ? void : $L});

mutable workAllowed:=true;
mutable alreadyExamined:=[].toSet;
mutable runningTasks:=0;
datastore SEVEN_ZIP_PATH:=void;

blocked->begin
  local scanInterval:=30;
  local lastQuery:=scriptTime-scanInterval;
  local lastResponse:=[];
save;
  scriptTime+scanInterval>lastQuery
  ? begin
      lastResponse:=runningGravTasks.map((n)->n.changeFileExt('')).relativeFileName.toSet;
      lastQuery:=scriptTime;
    end
  : void;
  lastResponse;
end;

@SuppressUnusedWarning
ensure7zp:=SEVEN_ZIP_PATH.isVoid OR !fileExists(SEVEN_ZIP_PATH)
? SEVEN_ZIP_PATH:=begin
    log("Paths of 7z.exe is not set.\nThis may take a while...");
    local all:=allFiles(folders('C:\*prog*')|getEnv['USERPROFILE'],'7z.exe');
    all.sort((x,y)->x.length<=y.length).head;
  end
: void;

compressSet(s,wait:Boolean)->(!wait OR (workAllowed AND s not in alreadyExamined)) AND begin
  alreadyExamined|=s;
  local _log:=wait ? {tPrint@$params}
                   : (...)->void;
  local set:=files(['.anim','.exe','.dump'].map((e)->s.changeFileExt(e))).sort;
  local targetMnh:=s.changeFileExt('.mnh');
  fileExists(targetMnh) AND (set.size==3) ? begin
    _log('Executing ',targetMnh,' check');
    execPipeless(executor,[targetMnh.expandedFileName,'check']);
  end : void;
  local targetZip:=s.changeFileExt('.dat');
  set.size==3                                               ? void : return _log('Set ',s,' is incomplete') orElse false;
  (!fileExists(targetZip) OR !fileExists(targetMnh))        ? void : return _log('Set ',s,' is already compressed') orElse false;
  (set[2].expandedFileName.relativeFileName not in blocked) ? void : return _log('Set ',s,' is blocked') orElse false;
  deleteFile(targetZip);

  wait ? waitForFreeCapacity()() : void;
  workAllowed OR !wait ? begin
    _log('Writing ',targetZip);
    runningTasks+=1;
    local execResult:=exec(SEVEN_ZIP_PATH,['u','-mx9','-mmt1','-sfx',targetZip]|set);
    runningTasks-=1;
    execResult[1]==0
    ? void
    : return _log('Failed for set: ',s,"\n",join(execResult[0],"\n"));

    _log('Writing ',targetMnh);
    local linesToWrite:=[
       'main("check")->fileInfo('+set[0].extractFileName.escape+')["size"]>'&fileInfo(set[0])['size']&' OR !fileExists(myPath.changeFileExt(".dat")) ? begin deleteFile(myPath.changeFileExt(".dat")); deleteFile(myPath); end : void;',
       'main->'&set.each(n,format('fileExists(%s)',n.extractFileName.escape)).join(' AND ')&' ? main("check") : begin changeDirectory(myPath.extractFileDirectory); execPipeless(myPath.changeFileExt(".dat")); end;'];
    writeFileLines(targetMnh,linesToWrite);
    _log('Done (set: ',s,') running: ',runningTasks);
    true;
  end : false;
end;

pendingCompressions->allFiles('.','*.anim').fileInfo.map((f)->begin
  //Filter by anim file...
  local resolution:=f['path'].extractFileNameOnly.clean(['0'..'9'],'').softCast;
  resolution in [32,64,128,256] AND f['size']=(sqr(resolution)*2+8)*5000
  ? f['path'].changeFileExt('')
  : void;
end).filter((s)->s not in alreadyExamined).unique;

synchronized gravExes->begin
  local result:=void;
  local resultTime:=-600;
save;
  scriptTime>resultTime+60
  ? begin
      result:=allFiles('.','grav*.exe').map(::expandedFileName).map(::systemSpecificFilename);
      resultTime:=scriptTime;
    end
  : void;
  result;
end;

memoized mnhFiles->allFiles('.','*.mnh').filter((f)->f.extractFileDirectory<>'.' AND f.matches('grav\d+\.mnh'));

private mutable runningGravTask__:=[-10];
private synchronized invalidateRunningGravTasks->runningGravTask__:=[-10];
synchronized runningGravTasks->begin
  scriptTime>runningGravTask__[0]+5
  ? begin
      runningGravTask__[1]:=getTaskInfo.map((i)->gravExes[!pos(gravExes,i['commandLine']).isInfinite][0]).toSet;
      runningGravTask__[0]:=scriptTime;
    end
  : void;
  runningGravTask__[1];
end;

mutable toClear:=0;
tPrint(...)->begin
  local out:=join([formatTime("hh:mm.ss",systime),' ']|...);
  out&=repeat(' ',toClear-out.length);
  print("\r",out);
  toClear:=0;
end;

tPrintD(...)->begin
  local lastPrinted:=void;
save;
  lastPrinted==... ? void : begin
    local out:=join([formatTime("hh:mm.ss",systime),' ']|...);
    out&=repeat(' ',toClear-(toClear:=out.length));
    printDirect("\r",out);
    lastPrinted:=...;
  end;
end;

BLOCKS:=[0..100].map((l)->[" ","▁","▂","▃","▄","▅","▆","▇","█"][floor(l*9/101)])|'!';

SKIP:='skip';
FORCE:='force';
QUIT:='quit';

exeSorting(L:StringCollection)->L.map((n)->n.extractFileNameOnly.clean(['0'..'9'],'').softCast=>n).sort(1).sort(0).getInner(1);

start(replay:Boolean)->begin
  assertUniqueInstance;
  local consoleWidth:=exec('cmd',['/C','mode','con'])[0].filter((line)->line.matches(': *\d+$')).head(2).trailing.split(':')[1].trim.softCast-1;
  local firstQuestion:=true;
  local userSignal:='';
  localAsync({while(true,begin
    userSignal:=ask('Next element in queue...',[SKIP,FORCE,QUIT],firstQuestion);
    firstQuestion:=false;
  end)});
  local compressionFutures:=[];

  local startedCount:=0;

  local getNextTask:=toIteratableExpression(()->begin
    local pendingExe:=gravExes.exeSorting;
    local tasks:=[];
  save;
    tasks.size = 0
    ? begin
        tasks:=pendingCompressions;
        tasks.size = 0
        ? begin
            tasks|=pendingExe.head;
            pendingExe:=pendingExe.tail;
          end
        : void;
      end
    : void;
    local result:=userSignal==QUIT ? generatorClosed : begin
      tasks.size=0
      ? generatorClosed
      : tasks.head;
    end;
    tasks:=tasks.tail;
    result;
  end);


  getNextTask
  .each(execName,execName in runningGravTasks ? tPrint('          ',execName.relativeFileName,' is already running') :
  begin
    local isExe:=execName.extractFileExt='.exe';
    local name:=isExe
                ? execName.relativeFileName
                : f'{execName}->C';
    local startMethod:=isExe
                       ? ()->execute(execName,replay)
                       : ()->begin
                               alreadyExamined|=execName;
                               compressionFutures|=[future(::compressSet,[execName,false])];
                             end;

    userSignal:=userSignal==QUIT ? QUIT : '';
    local wait:=waitForFreeCapacity;
    local load:=getCpuLoadHistory(consoleWidth-length(name)-29);
    while((!peekFuture(wait) OR
           (runningGravTasks.size>=MAX_GRAV_INSTANCES) AND isExe) AND (userSignal=='') ,begin
      tPrintD('Next:     ',name,"; CPU: ",BLOCKS[load].join,format('%3d',load.trailing),'%');
      load:=getCpuLoadHistory(consoleWidth-length(name)-29);
      sleep(0.1);
    end);
    userSignal in [SKIP,QUIT]
    ? tPrint('Skipping: ',name)
    : begin
        userSignal==FORCE
        ? tPrint('Forced:   ',name)
        : tPrint('Starting: ',name);
        startMethod();
        startedCount+=1;
        invalidateRunningGravTasks;
      end;
  end);
  tPrint(startedCount,' tasks started');
  tPrint(runningGravTasks.size,' tasks are running: ',join("\n  "&sort(runningGravTasks)));
  compressionFutures.each(fut,peekFuture(fut) ? void : tPrint('Waiting for completion of compression future'),fut());
end;

//*Start in calculation mode
main->start(false);

//*Start in replay mode
main('r')->start(true);

//*Decompress all compressed files
main('d')->mnhFiles.pEach(script,begin
  print('Starting ',script);
  assert(exec(executor,[script])[1]==0,'Script ',script,' failed');
  print(script,' executed successfully');
end);

//*Decompress compressed files matching pattern
main('d',pattern)->mnhFiles.filter((script)->script.matches(pattern)).pEach(script,begin
  print('Starting ',script);
  waitForFreeCapacity()();
  assert(exec(executor,[script])[1]==0,'Script ',script,' failed');
  print(script,' executed successfully');
end);

//*List executables (exe) and compressed executables (mnh)
main('list')->begin
  local f:=(gravExes | mnhFiles).expandedFileName.relativeFileName;
  f:=[f.extractFileDirectory,f.extractFileName].transpose;
  local allNames      :=f.getInner(1).unique;

  f.getInner(0).unique.each(dir,join([dir,': ']|allNames.map((n)->[dir,n] in f ? n : ''),"\t"))
   .join("\n").print;
end;

watcher(filename:String)->{begin
  local timedOut:=true;
  local trailSize:=100;
  local trail:=[];
  local reinitialize:=()->begin
    timedOut:=false;
    trail:=[];
    localAsync({begin
      local it:=fileLineIterator(filename,60);
      local n:=void;
      while(!isVoid(n:=it()),begin
        trail:=
        (n.matches('Calculation thread stopped')
        ? trail.trailing|("\t"&n)
        : trailing(trail|("\t"&n),trailSize));
      end);
      trail|=("\t"&'...timed out');
      timedOut:=true;
    end});
  end;
save;
  timedOut ? reinitialize() : void;
  trailSize:=max(1,$trailSize);
  trail.size>trailSize
  ? trail.trailing(trailSize)
  : trail;
end};


//*Watch all log files
main('w')->begin
  local killSignal:=false;
  local linesTotal:=exec('cmd',['/C','mode','con'])[0].filter((line)->line.matches(': *\d+$')).head.split(':')[1].trim.softCast-1;
  local cpuWidth  :=32;

  localAsync({begin local answer:=''; while(answer!=QUIT,begin
    answer:=ask('?',[QUIT,'+','-'],false);
    answer=='+' ? printDirect("\rLines: ",linesTotal+=1,'  ') :
    answer=='-' ? printDirect("\rLines: ",linesTotal:=max(1,linesTotal-1),'  ') :
    answer==QUIT ? killSignal:=true : void;
  end); end});
  local logs:=runningGravTasks
    .sort((exeName)->occupiedIndexes[exeName] orElse Inf)
    .filter((e)->e.changeFileExt('.log').fileExists)
    .map((e)->begin
       local key:=e.relativeFileName.changeFileExt('');
       [key,watcher(e.changeFileExt('.log')),['-']];
     end);

  sleep(1);

  local nextScanForNewTasks:=scriptTime+10;
  local sleepTime:=0;
  while(!killSignal,begin
    local anyChanged:=false;
    scriptTime>=nextScanForNewTasks
    ? begin
        local currentRunning:=runningGravTasks.sort((exeName)->occupiedIndexes[exeName] orElse Inf).filter((e)->e.changeFileExt('.log').fileExists);
        currentRunning.map((e)->e.relativeFileName.changeFileExt('')).sort == logs.getInner(0).sort
        ? void
        : begin
            local previousEntries:=logs.map((l)->l[0]=>l).toMap;
            logs:=currentRunning
                  .map((e)->begin
                     local key:=e.relativeFileName.changeFileExt('');
                     previousEntries[key] orElse [key,watcher(e.changeFileExt('.log')),['']];
                   end);
            anyChanged:=true;
          end;
        nextScanForNewTasks:=scriptTime+10;
      end
    : void;

    logs:=logs.each(e,
      begin
        (local n:=e[1](linesTotal))==e[2]
        ? e
        : begin
            anyChanged:=true;
            [e[0],e[1],n];
          end;
      end);

    anyChanged
    ? begin
        local out:=logs.map((l)->[l[0],l[2]==[] ? [''] : l[2]]);
        out.size==0 ? void : begin
          local includeEta:=linesTotal>=3*out.size;


          local lines:=out.getInner(1).map((L)->max(1+ord(includeEta),L.size)).agg(+);

          local eta:=
          includeEta
          ? out.getInner(1).each(L,begin
              local remainingDays:=1/(24*60*60)*
              (4999-L.matchComposite('Step \d+ done'  ).getInner(0).trailing[0].orElse('0').clean(['0'..'9'    ],'').softCast)
                  *(L.matchComposite('done: \d+.\d+s;').getInner(0).getInner(0)            .clean(['0'..'9','.'],'').softCast.{$L.agg(+)/$L.size});

              remainingDays<1
              ? format("eta %s",formatTime('hh:mm:ss',remainingDays))
              : format("eta %s + %sdays",formatTime('hh:mm:ss',remainingDays-floor(remainingDays)),floor(remainingDays));
            end)
         : void;

          while(lines>max(linesTotal,out.size),begin
            local k:=argMax(out.getInner(1).map(::size));
            out[k]:=begin
                      lines-=1;
                      [out[k,0],out[k,1].tail];
                    end;
          end);

          cpuWidth:=1+out.getInner(0)        .map(::length).max
                     +out.getInner(1).flatten.map(::length).max
                   -9;
          includeEta
          ? out:=out.each(o,o[0]&o[1].head,eta[index]&(o[1,1]),o[1].tail(2),|)
          : out:=out.each(o,o[0]&o[1].head,o[1].tail,|);
        end;
        while(out.size<linesTotal,out|="\t...");
        printf('%s',"\f"|out);
      end
    : void;
    printDirect("\rCPU ",BLOCKS[getCpuLoadHistory(cpuWidth)].join,getCpuLoadHistory.orElse(['?']).trailing,'% ');

    sleep(anyChanged ? sleepTime:=0.5 : sleepTime:=2-(2-sleepTime)*0.9);
  end);
end;
