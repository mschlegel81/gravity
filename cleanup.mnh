USE util;

census->begin
  info:=allFiles('.').fileInfo;
  [info.size,info.getInner('size').agg(+)];
end;

exeMayBeDeleted(name:String)->
  name.changeFileExt('.dump').fileExists
  ? false
  : name.changeFileExt('.replay').fileExists
    ? true
    : false;

getListingToDelete(includeExe:Boolean)->begin
  changeDirectory(myPath.extractFileExt);
  blocked:=forcedRunningGravTasks.map((n)->n.changeFileExt('')).relativeFileName.toSet;
  unblockedFilter:=(f)->f.changeFileExt('').expandedFileName.relativeFileName not in blocked;
  union(allFiles('backup','*.*'),
        allFiles('lib','*.*'),
        files(['*.replay','*.anim','*.log','*.exe','*.dump']),
        allFiles('.','grav*.log'),
        allFiles('.','grav*.anim').intersect(allFiles('.','grav*.replay').changeFileExt('.anim')),
        includeExe ? allFiles('.','grav*.exe').filter(::exeMayBeDeleted) : void)  .filter(unblockedFilter);
end.sort;

cleanup(includeExe:Boolean)->begin
  oldCensus:=census;
  getListingToDelete(includeExe).agg((f)->log('Deleting ',f,deleteFile(f) ? ' o.k.' : ' FAILED'));
  freed:=oldCensus-census;
  log( floor(freed[1]      ),"\tbytes in ",freed[0],' files freed',
  "\n",floor(freed[1]/1024 ),"\tkiB",
  "\n",floor(freed[1]/1024²),"\tMiB",
  "\n",floor(freed[1]/1024³),"\tGiB");
end;

main->cleanup(false);
//*Cleanup, including exe files
main('deep')->cleanup(true);
//*List files that would be deleted
main(       'preview')->printf('%s',getListingToDelete(false));
//*List files that would be deleted in deep run
main('deep','preview')->printf('%s',getListingToDelete(true));
